------------------------------------------------------------------------------------------------------------------------
this module implements the Bundle concept for the YAPL lexer
------------------------------------------------------------------------------------------------------------------------
module yapl.v4.examples.lexer.impl.bundle


imports
========================================================================================================================

    from runtime.operating_system.file_system
        import DirectoryPath, FilePath

    from .
        manifest
            import Manifest
        file_reference
            import FileReference

exports
========================================================================================================================

    Bundle


contains
========================================================================================================================

    --------------------------------------------------------------------------------------------------------------------
    Class definition for a YAPL bundle
    --------------------------------------------------------------------------------------------------------------------
    class Bundle

        public instance initializers
        ================================================================================================================

            ------------------------------------------------------------------------------------------------------------
            Initialise the Bundle instance
            ------------------------------------------------------------------------------------------------------------
            initialise instance
                accepts
                    bundle_directory:DirectoryPath{absolute} -- the directory that shall contain the bundle files
                body
                    bundle_directory = parameters.bundle_directory
                    bundle_directory.create_if_missing(readable=all, writable=owner)


        public instance methods
        ================================================================================================================

            ------------------------------------------------------------------------------------------------------------
            Loads our bundle manifest from the filesystem
            ------------------------------------------------------------------------------------------------------------
            method load
                returns
                    manifest:Manifest -- the manifest that was loaded
                initializes
                    manifest
                body
                    manifest = Manifest(bundle_directory)
                    manifest ~= manifest.load()
                    return manifest = manifest

            ------------------------------------------------------------------------------------------------------------
            Saves our bundle manifest to the filesystem
            ------------------------------------------------------------------------------------------------------------
            method save
                body
                    manifest ~= manifest.save()

            ------------------------------------------------------------------------------------------------------------
            Adds a source file reference to our bundle. The manifest will be updated, and the source file will be
            copied to our bundle's source files directory
            ------------------------------------------------------------------------------------------------------------
            method add_source
                accepts
                    source:FileReference -- the source file reference to add
                body
                    manifest ~= manifest.add_source(source)
                    destination_root_directory = bundle_directory.subdirectory("source_files")
                    destination = destination_root_directory.relative_path_to_file(source.relative_filename)
                    destination.directory.create_if_missing(readable=all, writable=owner)
                    source.copy_file_to(destination)


        private instance state
        ================================================================================================================

            ------------------------------------------------------------------------------------------------------------
            The filesystem directory that our bundle files are stored in
            ------------------------------------------------------------------------------------------------------------
            bundle_directory:DirectoryPath{absolute}

            ------------------------------------------------------------------------------------------------------------
            The manifest describing our bundle. The manifest shall reside in the bundle directory.
            ------------------------------------------------------------------------------------------------------------
            manifest:Manifest{defer initialization, mutable}

